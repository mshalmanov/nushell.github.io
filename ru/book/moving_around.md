# Перемещение по системе

Одной из ключевых характеристик оболочки является возможность навигации и взаимодействия с файловой системой. Nushell, конечно же, не является исключением. Вот несколько распространенных команд, которые вы можете использовать при работе с файловой системой:

## Просмотр содержимого директории

```nu
ls
```

Как показано в разделе "Быстрый тур", команда [`ls`](/commands/docs/ls.md) возвращает содержимое директории. Команда `ls` в Nushell возвращает содержимое в виде [таблицы](types_of_data.html#tables).

Команда [`ls`](/commands/docs/ls.md) также принимает необязательный аргумент для изменения того, что вы хотите просмотреть. Например, мы можем перечислить файлы, которые заканчиваются на ".md":

```nu
ls *.md
# => ╭───┬────────────────────┬──────┬──────────┬──────────────╮
# => │ # │        name        │ type │   size   │   modified   │
# => ├───┼────────────────────┼──────┼──────────┼──────────────┤
# => │ 0 │ CODE_OF_CONDUCT.md │ file │  3.4 KiB │ 9 months ago │
# => │ 1 │ CONTRIBUTING.md    │ file │ 11.0 KiB │ 5 months ago │
# => │ 2 │ README.md          │ file │ 12.0 KiB │ 6 days ago   │
# => │ 3 │ SECURITY.md        │ file │  2.6 KiB │ 2 months ago │
# => ╰───┴────────────────────┴──────┴──────────┴──────────────╯
```

## Глобальные шаблоны (Подстановочные знаки)

Звездочка (`*`) в приведенном выше необязательном аргументе `*.md` иногда называется подстановочным знаком или глобом. Она позволяет нам сопоставлять любые значения. Вы можете прочитать этот глоб `*.md` как _"совпадение с любым именем файла, если оно заканчивается на '.md'."_

Наиболее общим шаблоном глоб является `*`, который будет соответствовать всем путям. Чаще всего этот шаблон используется как часть другого шаблона, например, `*.bak` и `temp*`.

Nushell также поддерживает двойную звездочку `**`, которая будет просматривать пути, вложенные в другие директории. Например, `ls **/*` выведет список всех не скрытых путей, вложенные в текущую директорию.

```nu
ls **/*.md
# => ╭───┬───────────────────────────────┬──────┬──────────┬──────────────╮
# => │ # │             name              │ type │   size   │   modified   │
# => ├───┼───────────────────────────────┼──────┼──────────┼──────────────┤
# => │ 0 │ CODE_OF_CONDUCT.md            │ file │  3.4 KiB │ 5 months ago │
# => │ 1 │ CONTRIBUTING.md               │ file │ 11.0 KiB │ a month ago  │
# => │ 2 │ README.md                     │ file │ 12.0 KiB │ a month ago  │
# => │ 3 │ SECURITY.md                   │ file │  2.6 KiB │ 5 hours ago  │
# => │ 4 │ benches/README.md             │ file │    249 B │ 2 months ago │
# => │ 5 │ crates/README.md              │ file │    795 B │ 5 months ago │
# => │ 6 │ crates/nu-cli/README.md       │ file │    388 B │ 5 hours ago  │
# => │ 7 │ crates/nu-cmd-base/README.md  │ file │    262 B │ 5 hours ago  │
# => │ 8 │ crates/nu-cmd-extra/README.md │ file │    669 B │ 2 months ago │
# => │ 9 │ crates/nu-cmd-lang/README.md  │ file │  1.5 KiB │ a month ago  │
# => ╰───┴───────────────────────────────┴──────┴──────────┴──────────────╯
```

Здесь мы ищем любой файл, который заканчивается на ".md". Двойная звездочка дополнительно указывает _"в любой директории, начиная с текущей."_

Синтаксис глобов в Nushell поддерживает не только `*`, но и сопоставление [одиночных символов с помощью `?` и групп символов с помощью `[...]`](https://docs.rs/nu-glob/latest/nu_glob/struct.Pattern.html).

Исключение символов `*`, `?` и `[]` осуществляется путем заключения их в одинарные, двойные кавычки или [сырой строковый литерал](working_with_strings.md#raw-strings). Например, чтобы показать содержимое директории с именем `[slug]`, используйте `ls "[slug]"` или `ls '[slug]'`.

Однако строки в обратных кавычках не экранируют глоб. Например, сравните следующие сценарии:

1. Без кавычек: Глоб-шаблон

   Необработанная [строка без кавычек](working_with_strings.html#bare-word-strings) с символами глоба интерпретируется как глоб-шаблон, поэтому следующая команда удалит все файлы в текущей директории, содержащие `myfile` в любом месте имени файла:

   ```nu
   rm *myfile*
   ```

2. В кавычках: Строковый литерал с символами звездочки

   При использовании одинарных или двойных кавычек, либо [сырого строкового литерала](working_with_strings.html#raw-strings), передается _строка_ с буквальными, экранированными звездочками (или другими символами глоба). Результат не является глобом. Следующая команда удалит только файл с буквальным именем `*myfile*` (включая звездочки). Другие файлы с `myfile` в имени не будут затронуты:

   ```nu
   rm "*myfile*"
   ```

3. В обратных кавычках: Глоб-шаблон

   Звездочки (и другие глоб-шаблоны) внутри [строки в обратных кавычках](working_with_strings.html#backtick-quoted-strings) интерпретируются как глоб-шаблон. Обратите внимание, что это такое же поведение, как и в примере со строкой без кавычек в пункте #1 выше.

   Следующая команда, как и в первом примере, удаляет все файлы в текущей директории, содержащие `myfile` в имени файла:

   ```nu
   rm `*myfile*`
   ```

::: tip
Nushell также включает специальную команду [`glob`](https://www.nushell.sh/commands/docs/glob.html) с поддержкой более сложных сценариев глобинга.
:::

### Преобразование строк в глоб-шаблоны

Приведенные выше методы кавычек полезны при создании глоб-литералов, но вам может понадобиться создавать глоб-шаблоны программно. Для этого доступны несколько методов:

1. `into glob`

   Команда [`into glob`](/commands/docs/into_glob.html) может быть использована для преобразования строки (и других типов) в глоб. Например:

   ```nu
   # Найти файлы, в имени которых включает текущий месяц в формате YYYY-mm
   let current_month = (date now | format date '%Y-%m')
   let glob_pattern = ($"*($current_month)*" | into glob)
   ls $glob_pattern
   ```

2. Оператор spread в сочетании с командой [`glob`](/commands/docs/glob.html):

   Команда [`glob`](/commands/docs/glob.html) (примечание: это не то же самое, что `into glob`) возвращает [`список`](types_of_data.html#lists) имен файлов, соответствующих глоб-шаблону. Этот список можно быть расширен и передан командам файловой системы с помощью [оператора разворачивания](operators.html#spread-operator):

   ```nu
   # Найти файлы, имя которых включает текущий месяц в формате YYYY-mm
   let current_month = (date now | format date '%Y-%m')
   ls ...(glob $"*($current_month)*")
   ```

3. Принудительное указание типа `glob` через аннотацию:

   ```nu
   # Найти файлы, имя которых включает текущий месяц в формате YYYY-mm
   let current_month = (date now | format date '%Y-%m')
   let glob_pattern: glob = ($"*($current_month)*")
   ls $glob_pattern
   ```

## Создание директории

Как и в большинстве других оболочек, команда [`mkdir`](/commands/docs/mkdir.md) используется для создания новых директорий. Одно из отличий заключается в том, что внутренняя команда `mkdir` в Nushell по умолчанию работает как `mkdir -p` в Unix/Linux, то есть:

- Автоматически создает несколько уровней директорий. Например:

  ```nu
  mkdir modules/my/new_module
  ```

  Это создаст все три директории, даже если ни одна из них в данный момент не существует. В Linux/Unix для этого требуется `mkdir -p`.

- Не вызывает ошибку, если директория уже существует. Например:

  ```nu
  mkdir modules/my/new_module
  mkdir modules/my/new_module
  # => No error
  ```

  ::: tip
  Распространенная ошибка при переходе на Nushell — является попытка использовать `mkdir -p <directory>` как в родной версии Linux/Unix. Однако это вызовет ошибку `Unknown Flag` в Nushell.

  Просто повторите команду без `-p`, чтобы добиться того же эффекта.
  :::

## Изменение текущей директории

```nu
cd cookbook
```

Чтобы перейти из текущей директории в новую, используйте команду [`cd`](/commands/docs/cd.md).

Изменение текущей рабочей директории также можно выполнить, если опустить [`cd`](/commands/docs/cd.md) и указать путь напрямую:

```nu
cookbook/
```

Как и в других оболочках, вы можете использовать либо имя директории, либо, если вы хотите подняться на уровень выше, использовать сокращение `..`.

Вы также можете добавить дополнительные точки, чтобы подняться на несколько уровней выше:

```nu
# Перейти в родительскую директорию
cd ..
# или
..
# Подняться на два уровня выше (родитель родителя)
cd ...
# или
...
# Подняться на три уровня выше (родитель родителя родителя)
cd ....
# И так далее.
```

::: tip
Сокращения с несколькими точками доступны как для внутренних [команд файловой системы](/commands/categories/filesystem.html) Nushell, так и для внешних команд. Например, выполнение `^stat ....` в системе Linux/Unix покажет, что путь расширяется до `../../../..`
:::

Вы можете комбинировать относительные уровни директорий с именами директорий:

```nu
cd ../sibling
```

::: tip ВАЖНО
Изменение директории с помощью [`cd`](/commands/docs/cd.md) изменяет переменную окружения `PWD`. Это означает, что изменение директории сохраняется в текущей области видимости (например, блоке или замыкании). После выхода из блока вы вернетесь в предыдущую директорию. Подробнее об этом вы можете узнать в главе [Окружение](./environment.md).
:::

## Команды файловой системы

Nu также предоставляет некоторые базовые [команды файловой системы](/commands/categories/filesystem.html), которые работают кроссплатформенно, такие как:

- [`mv`](/commands/docs/mv.md) для переименования или перемещения файла или директории в новое место
- [`cp`](/commands/docs/cp.md) для копирования элемента в новое место
- [`rm`](/commands/docs/rm.md) для удаления элементов из файловой системы

::: tip ПРИМЕЧАНИЕ
В Bash и многих других оболочках большинство команд файловой системы (кроме `cd`) на самом деле являются отдельными двоичными файлами в системе. Например, в системе Linux команда `cp` — это двоичный файл `/usr/bin/cp`. В Nushell эти команды являются встроенными. Это имеет несколько преимуществ:

- Они работают одинаково на платформах, где бинарная версия может быть недоступна (например, Windows). Это позволяет создавать кроссплатформенные скрипты, модули и пользовательские команды.
- Они более тесно интегрированы с Nushell, что позволяет им понимать типы Nushell и другие конструкции.
- Как упоминалось в разделе [Быстрый тур](quick_tour.html), они документированы в справочной системе Nushell. Выполнение команды `help <command>` или `<command> --help` покажет документацию Nushell для данной команды.

Хотя обычно рекомендуется использовать встроенне версии Nushell, возможно также использовать двоичные файлы Linux. Подробности см. в разделе [Выполнение системных команд](./running_externals.md).
:::
